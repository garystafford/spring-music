apply plugin: 'java'
apply plugin: 'eclipse-wtp'
apply plugin: 'idea'
apply plugin: 'tomcat'
apply plugin: 'war'
apply plugin: 'cloudfoundry'

// versioning build artifacts
def major = '2'
def minor = System.env.TRAVIS_BUILD_NUMBER
minor = (minor != 'null') ? minor : System.env.SEMAPHORE_BUILD_NUMBER
minor = (minor != 'null') ? minor : '0'
def artifact_version = major + '.' + minor

def currentJvm = org.gradle.internal.jvm.Jvm.current()

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
            srcDir 'src/main/java'
        }
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = 'spring-music'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
      classpath group: 'org.gradle.api.plugins', name: 'gradle-tomcat-plugin', version: '1.2.5'
      classpath group: 'org.cloudfoundry', name: 'cf-gradle-plugin', version: '1.1.3'
    }
}

repositories {
    jcenter()
}

dependencies {
    // Spring and dependencies
    compile group: 'org.springframework', name: 'spring-context', version: springVersion
    compile group: 'org.springframework', name: 'spring-webmvc', version: springVersion

    // Spring Cloud
    compile group: 'org.springframework.cloud', name: 'spring-cloud-spring-service-connector', version: springCloudVersion
    compile group: 'org.springframework.cloud', name: 'spring-cloud-cloudfoundry-connector', version: springCloudVersion

    // Spring Data
    compile group: 'org.springframework.data', name: 'spring-data-jpa', version: '1.10.2.RELEASE'
    compile group: 'org.springframework.data', name: 'spring-data-redis', version: '1.7.2.RELEASE'
    compile group: 'org.springframework.data', name: 'spring-data-mongodb', version: '1.9.2.RELEASE'

    // JPA Persistence
    compile group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.1.1'
    compile group: 'org.hibernate', name: 'hibernate-entitymanager', version: '5.2.2.Final'
    runtime group: 'com.h2database', name: 'h2', version: '1.4.192'
    runtime group: 'mysql', name: 'mysql-connector-java', version: '6.0.3'
    runtime group: 'org.postgresql', name: 'postgresql', version: '9.4.1210.jre7'

    // Redis Persistence
    compile group: 'redis.clients', name: 'jedis', version: '2.9.0'

    // JSR-303 validation
    compile group: 'javax.validation', name: 'validation-api', version: '1.1.0.Final'
    compile group: 'org.hibernate', name: 'hibernate-validator', version: '5.2.4.Final'

    // Webjars
    compile group: 'org.webjars', name: 'bootstrap', version: '3.3.7-1'
    compile group: 'org.webjars', name: 'angularjs', version: '1.2.16'
    compile group: 'org.webjars', name: 'angular-ui', version: '0.4.0-3'
    compile group: 'org.webjars', name: 'angular-ui-bootstrap', version: '0.10.0-1'
    compile group: 'org.webjars', name: 'jquery', version: '3.1.0'

    // Jackson
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.2'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.2'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.2'

    // Logging
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.12'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.12'

    // Servlet
    compile group: 'javax.servlet', name: 'jstl', version: '1.2'
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.0.1'


    // Testing
    testCompile (group: 'junit', name: 'junit', version: '4.12') {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
    testCompile group: 'org.springframework', name: 'spring-test', version: springVersion
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testCompile group: 'com.jayway.jsonpath', name: 'json-path', version: '2.2.0'
    testCompile group: 'com.jayway.jsonpath', name: 'json-path-assert', version: '2.2.0'

    // Tomcat
    tomcat group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: tomcatVersion
    tomcat group: 'org.apache.tomcat.embed', name: 'tomcat-embed-logging-juli', version: tomcatVersion
    tomcat (group: 'org.apache.tomcat.embed', name: 'tomcat-embed-jasper', version: tomcatVersion) {
        exclude group: 'org.eclipse.jdt.core.compiler', module: 'ecj'
    }
}

war {
    // omit the version from the war file name
    version = ''
}

task warNoStatic(type: War) {
    // omit the version from the war file name
    version = ''
    exclude '**/assets/**'
    manifest {
        attributes    \
            'Manifest-Version': '1.0',    \
            'Created-By': currentJvm,    \
            'Gradle-Version': GradleVersion.current().version,    \
            'Implementation-Title': archivesBaseName + '.war',    \
            'Implementation-Version': artifact_version,    \
            'Implementation-Vendor': 'Gary A. Stafford'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

task warCopy(type: Copy) {
    from 'build/libs'
    into 'build/distributions'
    include '**/*.war'
}

task zipGetVersion(type: Task) {
    ext.versionfile = new File("${projectDir}/src/main/webapp/assets/buildinfo.properties")
    versionfile.text = 'build.version=' + artifact_version
}

task zipStatic(type: Zip) {
    from 'src/main/webapp/assets'
    appendix = 'static'
    version = ''
}

tomcatRun {
    outputFile = file('tomcat.log')
    configFile = file('src/main/webapp/META-INF/context.xml')
}

cloudfoundry {
    target = 'https://api.run.pivotal.io'
    space = 'development'

    file = file("${war.archivePath}")
    uri = "spring-music-${randomWord}.cfapps.io"
    memory = 512
    instances = 1

    services {
        'music-mongodb' {
            label = 'mongolab'
            plan = 'sandbox'
        }
    }
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}
